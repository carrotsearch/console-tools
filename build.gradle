
plugins {
  id 'com.diffplug.spotless' version "6.6.0" apply false
  id "com.palantir.consistent-versions" version "2.11.0"
}

apply from: file('gradle/min-gradle-version.gradle')
apply from: file('gradle/gitinfo.gradle')
apply from: file('gradle/buildinfo.gradle')

apply plugin: 'base'

def currentVersion = '1.1.3'

ext {
  gitRev = gitStatus.id

  licenseHeader = file('etc/license-header.txt')

  replaceTokens = [
    'version': currentVersion,
    'gitRev': gitRev,
    'buildDate': buildDate,
    'buildDateTrimmed': buildDateTrimmed
  ]

  isIdea = System.getProperty("idea.active") != null || gradle.startParameter.taskNames.contains('idea') || gradle.startParameter.taskNames.contains('cleanIdea')
}

allprojects {
  apply plugin: 'idea'

  group "com.carrotsearch.console"
  version = currentVersion

  repositories {
    mavenCentral()
  }

  idea {
    module {
      outputDir file('build/idea/classes/main')
      testOutputDir file('build/idea/classes/test')
      downloadSources = true
    }
  }
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'idea'
  apply plugin: 'com.diffplug.spotless'

  sourceCompatibility = 11
  targetCompatibility = 11

  spotless {
    java {
      licenseHeaderFile licenseHeader
      lineEndings 'UNIX'
      endWithNewline()
      googleJavaFormat('1.15.0')
    }
  }

  test {
    maxParallelForks = (int) Math.max(Runtime.runtime.availableProcessors() / 2.0, 4.0)
  }

  tasks.withType(JavaCompile) {
    // << "-Xlint:unchecked"
    options.compilerArgs << "-Xlint:deprecation"
    options.encoding = "UTF-8"
  }

  tasks.withType(Jar) {
    manifest {
      attributes("Implementation-Title": project.name,
                 "Implementation-Version": "${project.version} (${gitRev}, ${buildDateTrimmed})")
    }
  }

  javadoc {
    title = "${project.name} ${project.version} API Documentation"

    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
      options.addStringOption('Xdoclint:all,-missing', '-noindex')
    }

    options.encoding = 'UTF-8'
  }

  afterEvaluate {
    check.dependsOn(spotlessCheck)
    precommit.dependsOn(spotlessCheck)

    tasks.withType(AbstractArchiveTask) {
      duplicatesStrategy = 'fail'
    }
  }
}

task precommit {
  description "Runs code quality and other precommit checks"
  group "Verification"
}


subprojects {
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  def mavenBuildRepo = "${rootProject.buildDir}/maven"

  task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
  }

  publishing {
    repositories {
      maven {
        name = 'build'
        url = mavenBuildRepo
      }
      maven {
        name = 'sonatype'
        url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
        credentials {
            if (project.hasProperty('nexusUsername')) {
              username project.nexusUsername
            }
            if (project.hasProperty('nexusPassword')) {
              password project.nexusPassword
            }
        }
      }
    }

    publications {
      maven(MavenPublication) {
        afterEvaluate {
          from components.java
          artifactId = project.archivesBaseName

          artifact sourcesJar
          artifact javadocJar
        }

        pom {
          name = "${project.name}"
          description = "${project.name}"
          url = 'https://github.com/carrotsearch/console-tools'
          inceptionYear = "2019"
          licenses {
            license {
              name = 'BSD License'
              url = 'https://github.com/carrotsearch/console-tools/blob/master/LICENSE.txt'
            }
          }
          organization {
            name = "Carrot Search s.c."
            url = "https://www.carrotsearch.com"
          }
          developers {
            developer {
              id = 'stanislaw.osinski'
              name = 'Stanisław Osiński'
              email = 'stanislaw.osinski@carrotsearch.com'
            }
            developer {
              id = 'dawid.weiss'
              name = 'Dawid Weiss'
              email = 'dawid.weiss@carrotsearch.com'
            }
          }
          scm {
            connection = 'scm:git:git@github.com:carrotsearch/console-tools.git'
            developerConnection = 'scm:git:git@github.com:carrotsearch/console-tools.git'
            url = 'https://github.com/carrotsearch/console-tools'
          }
        }
      }
    }
  }

  signing {
    sign publishing.publications.maven
  }

  afterEvaluate {
    task mavenLocal(dependsOn: publishMavenPublicationToBuildRepository) {
      group "Publishing"
      description "Publish Maven artifacts locally to " + file(mavenBuildRepo)
    }

    task mavenSonatype(dependsOn: publishMavenPublicationToSonatypeRepository) {
      group "Publishing"
      description "Publish Maven artifacts to Sonatype OSS Nexus"
    }
  }
}
